apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release' // must be applied after your artifact generating plugin (eg. java / com.android.library)

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        warning 'MissingTranslation', 'ObjectAnimatorBinding', 'GetContentDescriptionOverride'
    }
}

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:27.1.1'
    compile 'com.android.support:support-v13:27.1.1'
    compile 'com.android.support:design:27.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.pholser:junit-quickcheck-core:0.7'
    testCompile 'com.pholser:junit-quickcheck-generators:0.7'

    androidTestCompile 'com.android.support:support-annotations:27.1.1'
    androidTestCompile 'com.android.support.test:runner:1.0.2'
    androidTestCompile 'com.android.support.test:rules:1.0.2'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

publish {
    userOrg = 'infinum'
    groupId = 'co.infinum'
    artifactId = 'materialdatetimepicker-support'
    publishVersion = project.VERSION_NAME
    desc = 'MaterialDateTimePicker which uses support fragments'
    website = 'https://github.com/infinum/MaterialDateTimePicker'
    licences = ["Apache-2.0"]
    repoName = 'android'
}